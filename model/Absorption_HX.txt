$UnitSystem SI K kPa kJ mass deg

m_a = 1 [kg/s]
x_c = 0.999 [kg/kg] "Ammonia mass fraction in the condensor stream"
x_a = 0.371 [kg/kg] "Ammonia mass fraction in the absorber out stream"
x_b = x_a - 0.1 [kg/kg] "Ammonia mass fraction in the other stream"

{T_con=40+273.15 [K]
T_eva=-10+273.15 [K]
T_abs=40+273.15 [K]
T_0=30+273.15 [K]
eff_p=0.5
T_gen = 130+273.15 [K]} "to match the example"
P_low=2.85 [bar]
P_high=15.5 [bar]
{Epsilon=1}

"Point#1"
CALL NH3H2O(138,T_abs,x_a,0: T_1, P_1, x_1, h_1, s_1, u_1, v_1, q_1)
m_a = m_b + m_c
m_a * h_1 + Q_abs = m_b * h_6 + m_c * h_10
m_a * x_a = m_b * x_b + m_c * x_c
S_abs = m_a * s_1 - m_b * s_6 - m_c * s_10 + Q_abs / T_0

"Point#2"
CALL NH3H2O(234,P_high,x_a,h_2a: T_2, P_2, x_2, h_2, s_2, u_2, v_2, q_2)
W_p_mech = m_a * v_1 * (P_high - P_low) * convert(bar, kPa)
m_a * h_1 + W_p_mech = m_a * h_2a
W_p_elec = W_p_mech / eff_p
S_p = m_a * (s_2 - s_1)

"HX"
m_b * (h_4 - h_5) = m_a * (h_3a - h_2)
S_hx = m_b * s_5 + m_a * s_3 - m_b * s_4 - m_a * s_2
Epsilon = (T_4 - T_5a) / (T_4 - T_2)

"Point#3"
CALL NH3H2O(234,P_high,x_a,h_3a: T_3, P_3, x_3, h_3, s_3, u_3, v_3, q_3)
S_gen = -m_a * s_3 + m_b * s_4 + m_c * s_7 - Q_gen / T_gen

"Point#4"
CALL NH3H2O(238,P_high,x_b,0: T_4, P_4, x_4, h_4, s_4, u_4, v_4, q_4)

"Point#5"
CALL NH3H2O(123,T_5a,P_high,x_b: T_5, P_5, x_5, h_5, s_5, u_5, v_5, q_5)

"Point#6"
CALL NH3H2O(234,P_low,x_b,h_5: T_6, P_6, x_6, h_6, s_6, u_6, v_6, q_6)
S_tv1 = m_b * (s_6 - s_5)

"Point#7"
CALL NH3H2O(238,P_high,x_c,1: T_7, P_7, x_7, h_7, s_7, u_7, v_7, q_7)
m_a * h_3 + Q_gen = m_b * h_4 + m_c * h_7

"Point#8"
CALL NH3H2O(128,T_con,P_high,0: T_8, P_8, x_8, h_8, s_8, u_8, v_8, q_8)
m_c * h_7 = m_c * h_8 + Q_con
S_con = m_c * (s_8 - s_7) + Q_con / T_0
S_tv2 = m_c * (s_9 - s_8)

"Point#9"
CALL NH3H2O(234,P_low,x_c,h_8: T_9, P_9, x_9, h_9, s_9, u_9, v_9, q_9)

"Point#10"
CALL NH3H2O(128,T_eva,P_low,1: T_10, P_10, x_10, h_10, s_10, u_10, v_10, q_10)
Q_eva + m_c * h_9 = m_c * h_10
S_eva = m_c * (s_10 - s_9) - Q_eva / T_0

"Output"
COP = Q_eva / (Q_gen + W_p_elec)
S_total = S_eva + S_tv2 + S_con + S_tv1 + S_gen + S_hx + S_p + S_abs
